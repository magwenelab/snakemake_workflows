# Base mosdepth workflow


import os.path
from pathlib import Path
import pandas as pd


configfile: "config/config.yaml"

SAMPLE_TABLE = pd.read_csv(config["mosdepth"]["sample_table"]).set_index("sample", drop=False)
SAMPLES = list(set(SAMPLE_TABLE["sample"]))

OUTDIR = config["output_directory"]
OUTPATH = Path(OUTDIR) / "mosdepth"


rule all:
    input:
# depth.mosdepth.global.dist.txt  depth.mosdepth.region.dist.txt 
        expand(OUTPATH / "{sample}/depth.mosdepth.global.dist.txt", sample=SAMPLES),
        expand(OUTPATH / "{sample}/depth.mosdepth.region.dist.txt", sample=SAMPLES),
        expand(OUTPATH / "{sample}/depth.mosdepth.summary.txt", sample=SAMPLES),
        expand(OUTPATH / "{sample}/depth.regions.bed.gz", sample=SAMPLES),
        expand(OUTPATH / "{sample}/depth.regions.bed.gz.csi", sample=SAMPLES),
        expand(OUTPATH / "softlinked/{sample}.summary.txt", sample=SAMPLES),
        expand(OUTPATH / "softlinked/{sample}.regions.bed.gz", sample=SAMPLES),
        expand(OUTPATH / "softlinked/{sample}.regions.bed.gz.csi", sample=SAMPLES)



def input_from_df(wildcards):
    s = SAMPLE_TABLE.loc[wildcards.sample,]
    return {
        "bam": s["bam"],
    }


rule mosdepth:
    input:
        unpack(input_from_df)
    output: 
        outdir = directory(OUTPATH / "{sample}"),
        dist_global = OUTPATH / "{sample}/depth.mosdepth.global.dist.txt",
        dist_region = OUTPATH / "{sample}/depth.mosdepth.region.dist.txt",
        summary = OUTPATH / "{sample}/depth.mosdepth.summary.txt",
        regions = OUTPATH / "{sample}/depth.regions.bed.gz",
        idx = OUTPATH / "{sample}/depth.regions.bed.gz.csi",
    params:
        prefix = lambda wildcards, output: Path(output.outdir) / "depth",
        window = config["mosdepth"]["window"],
        extra = config["mosdepth"]["extra_params"],   
    conda:
        "envs/mosdepth.yaml"
    threads: 
        config["mosdepth"]["threads"]        
    shell:
        "mkdir -p {output.outdir} "
        " && "
        "mosdepth -t {threads} -b {params.window} {params.extra} {params.prefix} {input.bam} "



rule make_links:
    input:  
        dist_global = rules.mosdepth.output.dist_global,
        dist_region = rules.mosdepth.output.dist_region,
        summary = rules.mosdepth.output.summary,
        regions = rules.mosdepth.output.regions,
        idx = rules.mosdepth.output.idx
    output:
        dist_global = OUTPATH / "softlinked/{sample}.global.dist.txt",
        dist_region = OUTPATH / "softlinked/{sample}.region.dist.txt",
        summary = OUTPATH / "softlinked/{sample}.summary.txt",  
        regions = OUTPATH / "softlinked/{sample}.regions.bed.gz",
        idx = OUTPATH / "softlinked/{sample}.regions.bed.gz.csi"
    shell:
        "ln -s -r {workflow.workdir_init}/{input.dist_global} {output.dist_global} "
        " && "
        "ln -s -r {workflow.workdir_init}/{input.dist_region} {output.dist_region} "
        " && "
        "ln -s -r {workflow.workdir_init}/{input.summary} {output.summary} "
        " && "
        "ln -s -r {workflow.workdir_init}/{input.regions} {output.regions} "
        " && "
        "ln -s -r {workflow.workdir_init}/{input.idx} {output.idx}"  